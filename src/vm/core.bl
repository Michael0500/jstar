class Number {
	def new() { error("Class Number cannot be instantiated."); }
	native __string__();
	native __class__();
	native __hash__();
}
class Boolean {
	def new() { error("Class Boolean cannot be instantiated."); }
	native __string__();
	native __class__();
}
class Null {
	def new() { error("Class Null cannot be instantiated."); }
	native __string__();
	native __class__();
}
class Function {
	def new() { error("Class Function cannot be instantiated."); }
	native __string__();
}
class Module   {
	def new() { error("Class Module cannot be instantiated."); }
	native __string__();
}

class String {
	def new() { error("Class String cannot be instantiated."); }
	native length();

	def __string__() {
		return this;
	}

	native __hash__();
}

class List {
	def new() { error("Class List cannot be instantiated."); }

	native add(e);
	native insert(i, e);
	native remove(i);
	native size();
	native clear();

	def addAll(iterable) {
		var changed = false;
		for(var e in iterable) {
			this.add(e);
			changed = true;
		}
		return changed;
	}

	def insertAll(i, iterable) {
		var changed = false;
		for(var e in iterable) {
			this.insert(i, e);
			changed = true;
		}
		return changed;
	}

	def delete(e) {
		var i = this.indexOf(e);
		if(i >= 0) {
			this.remove(i);
			return true;
		}
		return false;
	}

	def deleteAll(iterable) {
		var changed = false;
		for(var e in iterable) {
			var r = this.delete(e);
			changed = changed or r;
		}
		return changed;
	}

	def contains(e) {
		return this.indexOf(e) != -1;
	}

	def indexOf(e) {
		for(var i = 0; i < this.size(); i = i + 1) {
			if(this[i].__equals__(e)) {
				return i;
			}
		}
		return -1;
	}

	def indexOfLast(e) {
		for(var i = this.size() - 1; i >= 0; i = i - 1) {
			if(this[i].__equals__(e)) {
				return i;
			}
		}
		return -1;
	}

	def join(sep) {
		var s = "";
		for(var i = 0; i < this.size(); i = i + 1) {
			s = s + this[i].__string__();
			if(i != this.size() - 1) s = s + sep;
		}
		return s;
	}

	def subList(from, to) {
		if(to < from) {
			error("`to` must be greater or equal than `from`");
		}
		var sub = [];
		for(var i = from; i < to; i = i + 1) {
			sub.add(this[i]);
		}
		return sub;
	}

	def empty() {
		return this.size() == 0;
	}

	def __iterator__() {
		return __ListIterator(this);
	}

	def __string__() {
		return "[" + this.join(", ") + "]";
	}
}

class __ListIterator {
	def new(list) {
		this.n = 0;
		this.list = list;
	}

	def hasNext() {
		return this.n < this.list.size();
	}

	def next() {
		var next = this.list[this.n];
		this.n = this.n + 1;
		return next;
	}
}

def print(o) {
	__printstr(o.__string__());
}

def str(e) {
	return e.__string__();
}

native list(n, init);
native error(msg);
native isInt(n);
native __printstr(str);
