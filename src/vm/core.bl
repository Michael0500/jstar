class Number
	fun new()
		__noinstance(this)
	end
	native __string__()
	native __class__()
	native __hash__()
end
class Boolean
	fun new()
		__noinstance(this)
	end
	native __string__()
	native __class__()
end
class Null
	fun new()
		__noinstance(this)
	end
	native __string__()
	native __class__()
end
class Function
	fun new()
		__noinstance(this)
	end
	native __string__()
end
class Module
	fun new()
		__noinstance(this)
	end
	native __string__()
end
class StackTrace
	fun new()
		__noinstance(this)
	end
end

class String
	fun new()
		__noinstance(this)
	end

	native __eq__(o)
	native substr(from, to)
	native length()

	fun join(iterable)
		var lst = iterable
		if !(lst is List) then
			lst = []
			for var e in iterable do
				lst.add(e)
			end
		end
		return this.__join(lst)
	end

	native __join(lst)
	native __iter__(iter)
	native __next__(idx)

	fun __string__()
		return this
	end

	native __hash__()
end

class List
	fun new()
		__noinstance(this)
	end

	native add(e)
	native insert(i, e)
	native removeAt(i)
	native size()
	native clear()
	native subList(from, to)

	fun addAll(iterable)
		var changed = false
		for var e in iterable do
			this.add(e)
			changed = true
		end
		return changed
	end

	fun insertAll(iterable, i=0)
		if i < 0 or i >= this.size() then
			raise IndexOutOfBoundException(str(i))
		end

		var changed = false
		for var e in iterable do
			this.insert(i, e)
			changed = true
		end
		return changed
	end

	fun remove(e)
		for var i = 0; i < this.size(); i += 1 do
			if e == this[i] then
				this.removeAt(i)
				return true
			end
		end
		return false
	end

	fun removeAll(iterable)
		var changed = false
		for var e in iterable do
			var r = this.remove(e)
			changed = changed or r
		end
		return changed
	end

	fun contains(e)
		return this.indexOf(e) != -1
	end

	fun indexOf(e)
		for var i = 0; i < this.size(); i += 1 do
			if this[i] == e then
				return i
			end
		end
		return -1
	end

	fun indexOfLast(e)
		for var i = this.size() - 1; i >= 0; i -= 1 do
			if this[i] == e then
				return i
			end
		end
		return -1
	end

	fun empty()
		return this.size() == 0
	end

	fun swap(a, b)
		this[a], this[b] = this[b], this[a]
	end

	native __iter__(iter)
	native __next__(idx)

	fun __string__()
		var s = "["
		var length = this.size()
		
		for var i = 0; i < length; i += 1 do
			s += str(this[i])
			if i < length - 1 then s += ", " end
		end

		return s + "]"
	end

	fun __eq__(lst)
		if !(lst is List) then return false end
		
		if this.size() != lst.size() then
			return false
		end
		for var i in range(this.size()) do
			if this[i] != lst[i] then
				return false
			end
		end
		return true
	end
end

class Tuple
	fun new()
		__noinstance(this)
	end

	native size()

	fun __string__()
		var s = "("
		var size = this.size()

		for var i = 0; i < size; i += 1 do
			s += str(this[i])
			if(i != size - 1) then s += ", " end
		end

		return s + ")"
	end

	native __iter__(iter)
	native __next__(idx)

	fun __eq__(tup)
		if !(tup is Tuple) then return false end

		if this.size() != tup.size() then
			return false
		end
		for var i in range(this.size()) do
			if this[i] != tup[i] then
				return false
			end
		end
		return true
	end

end

fun __noinstance(inst)
	raise Exception("Class " + inst.__class__().getName() + " cannot be directly instantiated")
end

// Exceptions builtin classes

class Exception
	fun new(err=null)
		this.err = err
	end
end

class TypeException : Exception end
class NameException : Exception end
class FieldException : Exception end
class MethodException : Exception end
class ImportException : Exception end
class StackOverflowException : Exception end
class DivisionByZeroException : Exception end
class InvalidArgException : Exception end
class IndexOutOfBoundException : Exception end
class AssertException : Exception end
class IOException : Exception end

fun assert(cond, msg="assertion failed")
	if !cond then 
		raise AssertException(msg) 
	end
end

fun print(o="", file=null)
	write(o, file)
	__printstr("\n", file)
end

fun write(o, file=null)
	var s = str(o)
	assert(s is String, "__string__() didn't return a string, got " + s.__class__().getName())
	__printstr(s, file)
end

fun str(e)
	return e.__string__()
end

fun foreach(iter, pred)
	for var e in iter do
		pred(e)
	end
end

class range
	fun new(from, to=null, step=1)
		if to == null then 
			this.from, this.to = 0, from 
		else
			this.from, this.to = from, to
		end
		this.step = step
	end

	fun __iter__(iter)
		if !iter then
			return this.from + 1
		end

		return iter + this.step if iter < this.to else false
	end

	fun __next__(idx)
		return idx - 1
	end

	fun __string__()
		return "range(" + str(this.from) + ", " + str(this.to) + ")"
	end

end

native int(n)
native num(n)
native char(num)
native ascii(char)
native isInt(n)
native list(size=0, init=null)

native __printstr(str, file=null)
