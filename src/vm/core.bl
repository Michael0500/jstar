class Number
	fun new()
		__noinstance(this)
	end
	native __string__()
	native __class__()
	native __hash__()
end
class Boolean
	fun new()
		__noinstance(this)
	end
	native __string__()
	native __class__()
end
class Null
	fun new()
		__noinstance(this)
	end
	native __string__()
	native __class__()
end
class Function
	fun new()
		__noinstance(this)
	end
	native __string__()
end
class Module
	fun new()
		__noinstance(this)
	end
	native __string__()
end
class StackTrace
	fun new()
		__noinstance(this)
	end
end

class String
	fun new()
		__noinstance(this)
	end

	native __eq__(o)
	native substr(from, to)
	native length()

	fun join(iterable)
		var lst = iterable
		if !(lst is List) then
			lst = []
			for var e in iterable do
				lst.add(e)
			end
		end
		return this.__join(lst)
	end

	native __join(lst)

	fun __string__()
		return this
	end

	fun __iterator__()
		return __StringIterator(this)
	end

	native __hash__()
end

class __StringIterator
	fun new(str)
		this.str = str
		this.pos = 0
	end

	fun hasNext()
		return this.pos < this.str.length()
	end

	fun next()
		var c = this.str[this.pos]
		this.pos += 1
		return c
	end
end

class List
	fun new()
		__noinstance(this)
	end

	native add(e)
	native insert(i, e)
	native removeAt(i)
	native size()
	native clear()

	fun addAll(iterable)
		var changed = false
		for var e in iterable do
			this.add(e)
			changed = true
		end
		return changed
	end

	fun insertAll(iterable, i=0)
		if i < 0 or i >= this.size() then
			raise IndexOutOfBoundException(str(i))
		end

		var changed = false
		for var e in iterable do
			this.insert(i, e)
			changed = true
		end
		return changed
	end

	fun remove(e)
		var i = this.indexOf(e)
		if i >= 0 then
			this.remove(i)
			return true
		end
		return false
	end

	fun removeAll(iterable)
		var changed = false
		for var e in iterable do
			var r = this.remove(e)
			changed = changed or r
		end
		return changed
	end

	fun contains(e)
		return this.indexOf(e) != -1
	end

	fun indexOf(e)
		for var i = 0; i < this.size(); i += 1 do
			if this[i] == e then
				return i
			end
		end
		return -1
	end

	fun indexOfLast(e)
		for var i = this.size() - 1; i >= 0; i -= 1 do
			if this[i] == e then
				return i
			end
		end
		return -1
	end

	fun swap(a, b)
		var temp = this[a]
		this[a] = this[b]
		this[b] = temp
	end

	fun subList(from, to)
		if to < from then
			raise InvalidArgException("to arg should be >= from arg")
		end

		var sub = []
		for var i = from; i < to; i += 1 do
			sub.add(this[i])
		end
		return sub
	end

	fun empty()
		return this.size() == 0
	end

	fun sort()
		import rand
		this.__quickSort(0, this.size() - 1)
	end

	fun __quickSort(from, to)
		if from < to then
			var p = this.__partition(from, to)

			this.__quickSort(from, p - 1)
			this.__quickSort(p + 1, to)
		end
	end

	fun __partition(from, to)
		var p = rand.randint(from, to + 1)
		this.swap(from, p)

		var piv = this[from]
		var l = from + 1
		var r = to

		while l <= r do
			while this[r] > piv do
				r -= 1
			end
			while l <= r and this[l] <= piv do
				l += 1
			end

			if l < r then
				this.swap(l, r)
				l += 1
				r -= 1
			end
		end

		this.swap(from, r)
		return r
	end

	fun __iterator__()
		return __ListIterator(this)
	end

	fun __string__()
		var s = "["
		var length = this.size()
		
		for var i = 0; i < length; i += 1 do
			s += str(this[i])
			if i < length - 1 then s += ", " end
		end

		return s + "]"
	end

	fun __eq__(lst)
		if !(lst is List) then return false end
		
		if this.size() != lst.size() then
			return false
		end
		for var i in range(this.size()) do
			if this[i] != lst[i] then
				return false
			end
		end
		return true
	end
end

class __ListIterator
	fun new(list)
		this.n = 0
		this.list = list
	end

	fun hasNext()
		return this.n < this.list.size()
	end

	fun next()
		var next = this.list[this.n]
		this.n += 1
		return next
	end
end

class __ListReverseIterator
	fun new(list)
		this.n = list.size() - 1
		this.list = list
	end

	fun hasNext()
		return this.n >= 0
	end

	fun next()
		var prev = this.list[this.n]
		this.n -= 1
		return prev
	end

	fun __iterator__()
		return this
	end
end

fun reversed(lst)
	return __ListReverseIterator(lst)
end

fun __noinstance(inst)
	raise Exception("Class " + inst.__class__().getName() + " cannot be directly instantiated")
end

// Exceptions builtin classes

class Exception
	fun new(err=null)
		this.err = err
	end
end

class TypeException : Exception end
class NameException : Exception end
class FieldException : Exception end
class MethodException : Exception end
class ImportException : Exception end
class StackOverflowException : Exception end
class DivisionByZeroException : Exception end
class InvalidArgException : Exception end
class IndexOutOfBoundException : Exception end
class AssertException : Exception end
class IOException : Exception end

fun assert(cond, msg="assertion failed")
	if !cond then 
		raise AssertException(msg) 
	end
end

fun print(o="", file=null)
	write(o, file)
	__printstr("\n", file)
end

fun write(o, file=null)
	var s = str(o)
	assert(s is String, "__string__() didn't return a string, got " + s.__class__().getName())
	__printstr(s, file)
end

fun str(e)
	return e.__string__()
end

class range
	fun new(from, to=null, step=1)
		if to == null then
			to = from
			from = 0
		end

		if !isInt(from) or !isInt(to) then
			raise InvalidArgException("Argument `from` and `to` must be integers")
		end

		this.from = from
		this.to = to
		this.count = from
		this.step = step
	end

	fun hasNext()
		return this.count < this.to if this.step > 0 else this.count >= this.to
	end

	fun next()
		var i = this.count
		this.count += this.step
		return i
	end

	fun __iterator__()
		return this
	end

	fun __string__()
		var s = "["
		for var i = this.from; i < this.to; i += 1 do
			s += i.__string__()
			if i != this.to - 1 then
				s += ", "
			end
		end
		return s + "]"
	end
end

native int(n)
native num(n)
native char(num)
native ascii(char)
native isInt(n)
native list(size=0, init=null)

native __printstr(str, file=null)
