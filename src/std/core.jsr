// -----------------------------------------------------------------------------
// BUILTIN OBJECTS
// -----------------------------------------------------------------------------

var importPaths, argv = [], []

// -----------------------------------------------------------------------------
// BUILTIN CLASSES
// -----------------------------------------------------------------------------

class Number
    native new(n)
    native isInt()
    native __string__()
    native __hash__()
end

class Boolean
    native new(b)
    native __string__()
    native __hash__()
end

class Null
    native __string__()
end

class Function
    native __string__()
end

class Module
    native __string__()
end

class StackTrace end
class Userdata end

class Iter
    fun forEach(func)
        for var e in this do
            func(e)
        end
    end

    fun enumerate() 
        return IEnumerate(this)
    end

    fun map(func)
        return IMap(func, this)
    end

    fun filter(func) 
        return IFilter(func, this)
    end

    fun zip(iterable)
        return IZip(this, iterable)
    end
end

class Sequence is Iter
    fun contains(e)
        return this.indexOf(e) != -1
    end

    fun indexOf(e)
        var length = #this
        for var i = 0; i < length; i += 1 do
            if this[i] == e then
                return i
            end
        end
        return -1
    end

    fun indexOfLast(e)
        for var i = #this - 1; i >= 0; i -= 1 do
            if this[i] == e then
                return i
            end
        end
        return -1
    end

    fun count(e)
        var count = 0
        for var o in this do
            if o == e then 
                count += 1
            end
        end
        return count
    end

    fun empty()
        return #this == 0
    end

    fun reverse()
        return IReverse(this)
    end

    fun __eq__(other)
        if type(other) != type(this) then
            return false 
        end

        var length = #this
        if length != #other then
            return false
        end

        for var i = 0; i < length; i += 1 do
            if this[i] != other[i] then
                return false
            end
        end
        return true
    end
end

class String is Sequence
    native new(...)
    native charAt(idx)
    native startsWith(prefix, offset=0)
    native endsWith(suffix)
    native strip()
    native chomp()
    native join(iterable)
    native __mod__(args)
    native __eq__(o)
    native __len__()
    native __iter__(iter)
    native __next__(idx)
    native __string__()
    native __hash__()
end

class List is Sequence
    native new(n=0, init=null)
    native add(e)
    native insert(i, e)
    native removeAt(i)
    native clear()
    native sort(comparator=null)
    native __len__()
    native __eq__(other)
    native __iter__(iter)
    native __next__(idx)

    fun addAll(iterable)
        var changed = false
        for var e in iterable do
            this.add(e)
            changed = true
        end
        return changed
    end

    fun insertAll(iterable, i=0)
        var changed = false
        for var e in iterable do
            this.insert(i, e)
            changed = true
        end
        return changed
    end

    fun remove(e)
        var length = #this
        for var i = 0; i < length; i += 1 do
            if e == this[i] then
                this.removeAt(i)
                return true
            end
        end
        return false
    end

    fun removeAll(iterable)
        var changed = false
        for var e in iterable do
            var r = this.remove(e)
            changed = changed or r
        end
        return changed
    end

    fun pop()
        return this.removeAt(#this - 1)
    end

    fun __string__()
        return "[" + ", ".join(this) + "]"
    end
end

class Tuple is Sequence
    native new(iterable=null)
    native __len__()
    native __eq__(other)
    native __iter__(iter)
    native __next__(idx)
    native __hash__()

    fun __string__()
        return "(" + ", ".join(this) + ")"
    end
end

class Table is Iter
    native new(iterable=null)
    native delete(key)
    native clear()
    native contains(key)
    native keys()
    native values()
    native __get__(key)
    native __set__(key, val)
    native __len__()
    native __iter__(i)
    native __next__(i)
    native __string__()
end

class Enum
    native new(...)
    native value(name)
    native name(value)
end

// -----------------------------------------------------------------------------
// BUILTIN FUNCTIONS
// -----------------------------------------------------------------------------

fun assert(cond, msg="assertion failed", exception=null)
    if !cond then 
        raise (exception(msg) if exception else AssertException(msg))
    end
end

fun typeAssert(arg, cls, name)
    if !(arg is cls) then
        var got, expected = cls.getName(), type(arg).getName()
        raise TypeException("{0} must be a {1}, got {2}" % (name, got, expected))
    end
end

fun sorted(iterable, comparator=null)
    var lst = toList(iterable)
    lst.sort(comparator)
    return lst
end

fun reduce(iterable, fn)
    var iter = iterable.__iter__(null)
    if !iter then 
        return null 
    end

    var accu = iterable.__next__(iter) 
    while iter = iterable.__iter__(iter) do
        accu = fn(accu, iterable.__next__(iter))
    end

    return accu
end

fun any(iterable, predicate)
    for var e in iterable do
        if predicate(e) then
            return true
        end
    end
    return false
end

fun all(iterable, predicate)
    for var e in iterable do
        if !predicate(e) then
            return false
        end
    end
    return true
end

fun partial(fn, arg)
    return |...| => fn(arg, args)...
end

fun toList(iterable)
    if iterable is Sequence then
        return List(#iterable, |i| => iterable[i])
    else
        var list = []
        list.addAll(iterable)
        return list
    end
end

native ascii(num)
native char(c)
native eval(source)
native garbageCollect()
native int(n)
native print(s, ...)
native type(o)

// -----------------------------------------------------------------------------
// BUILTIN ITERATORS
// -----------------------------------------------------------------------------

class IReverse is Sequence
    fun new(sequence)
        this._sequence = sequence
        this._size = #sequence
    end

    fun __get__(i)
        try
            return this._sequence[this._size - i - 1]
        except IndexOutOfBoundException e
            raise IndexOutOfBoundException(##i)
        end
    end

    fun __len__()
        return this._size
    end

    fun __iter__(i)
        if i == null then
            return 0 if this._size > 0 else false
        end
        return i + 1 if i < this._size - 1 else false
    end

    fun __next__(i)
        return this._sequence[this._size - i - 1]
    end
end

class IEnumerate is Iter
    fun new(iter, start=0)
        this._iter = iter
        this._idx = start
    end

    fun __iter__(i)
        return this._iter.__iter__(i)
    end

    fun __next__(i)
        var ret = (this._idx, this._iter.__next__(i))
        this._idx += 1
        return ret
    end
end

class IFilter is Iter
    fun new(func, iterable)
        this._func = func
        this._iterable = iterable
    end

    fun __iter__(iter)
        while iter = this._iterable.__iter__(iter) do
            if this._func(this._iterable.__next__(iter)) then
                return iter
            end
        end
        return false
    end

    fun __next__(i)
        return this._iterable.__next__(i)
    end
end

class IMap is Iter
    fun new(func, iterable)
        this._func = func
        this._iterable = iterable
    end

    fun __iter__(i)
        return this._iterable.__iter__(i)
    end

    fun __next__(i)
        return this._func(this._iterable.__next__(i))
    end
end

class IZip is Iter
    fun new(...)
        if #args == 0 then 
            raise InvalidArgException("Expected at least one iterator.")
        end

        this._iterables = args
        this._result = []
    end

    fun __iter__(iters)
        var i = 0
        for var iterator in this._iterables do
            var res = iterator.__iter__(iters[i] if iters else null)
            if !res then 
                return false 
            end

            this._result.add(res)
            i += 1
        end

        var ret = Tuple(this._result)
        this._result.clear()
        
        return ret
    end

    fun __next__(iters)
        var i = 0
        for var iterator in this._iterables do
            this._result.add(iterator.__next__(iters[i]))
            i += 1
        end

        var ret = Tuple(this._result)
        this._result.clear()
        
        return ret
    end
end

// -----------------------------------------------------------------------------
// BUILTIN EXCEPTIONS
// -----------------------------------------------------------------------------

class Exception
    fun new(err="", cause=null)
        this._err = err
        this._cause = cause
        this._stacktrace = null
    end

    fun err()
        return this._err
    end

    fun cause()
        return this._cause
    end

    native printStacktrace()
    native getStacktrace()
end

class TypeException is Exception end
class NameException is Exception end
class FieldException is Exception end
class MethodException is Exception end
class ImportException is Exception end
class StackOverflowException is Exception end
class SyntaxException is Exception end
class InvalidArgException is Exception end
class IndexOutOfBoundException is Exception end
class AssertException is Exception end
class NotImplementedException is Exception end
class ProgramInterrupt is Exception end