//====================================
//========= Built-in classes =========
//====================================

class Number
    native __string__()
    native __hash__()
end
class Boolean
    native __string__()
end
class Null
    native __string__()
end
class Function
    native __string__()
end
class Module
    native __string__()
end
class StackTrace end

class String
    native substr(from, to)
    native startsWith(prefix, offset = 0)
    native endsWith(suffix)
    native join(iterable)

    native __eq__(o)
    native __len__()
    native __iter__(iter)
    native __next__(idx)
    native __string__()
    native __hash__()
end

class Sequence
    fun new()
        raise NotImplementedException('Sequence is an abstract class')
    end

    fun contains(e)
        return this.indexOf(e) != -1
    end

    fun indexOf(e)
        for var i = 0; i < #this; i += 1 do
            if this[i] == e then
                return i
            end
        end
        return -1
    end

    fun indexOfLast(e)
        for var i = #this - 1; i >= 0; i -= 1 do
            if this[i] == e then
                return i
            end
        end
        return -1
    end

    fun empty()
        return #this == 0
    end

    fun foreach(pred)
        for var e in this do pred(e) end
    end

    fun __eq__(o)
        if type(o) != type(this) then
            return false 
        end

        var length = #this
        if length != #o then
            return false
        end

        for var i = 0; i < length; i += 1 do
            if this[i] != o[i] then
                return false
            end
        end
        return true
    end
end

class List : Sequence
    native new(n=0, init=null)
    native add(e)
    native insert(i, e)
    native removeAt(i)
    native subList(from, to)
    native clear()
    native __len__()
    native __iter__(iter)
    native __next__(idx)

    fun addAll(iterable)
        var changed = false
        for var e in iterable do
            this.add(e)
            changed = true
        end
        return changed
    end

    fun insertAll(iterable, i=0)
        if i < 0 or i >= #this then
            raise IndexOutOfBoundException(str(i))
        end

        var changed = false
        for var e in iterable do
            this.insert(i, e)
            changed = true
        end
        return changed
    end

    fun remove(e)
        for var i = 0; i < #this; i += 1 do
            if e == this[i] then
                this.removeAt(i)
                return true
            end
        end
        return false
    end

    fun removeAll(iterable)
        var changed = false
        for var e in iterable do
            var r = this.remove(e)
            changed = changed or r
        end
        return changed
    end

    fun sort(comp=null)
        var size = #this - 1
        var temp = List(#this, fun(i) return this[i] end)
        for var m = 1; m <= size; m *= 2 do
            for var i = 0; i < size; i += 2 * m do
                var from, mid, to = i, i + m - 1, i + 2 * m - 1
                if to > size then to = size end
                this.__merge(temp, from, mid, to, comp)
            end
        end
    end

    fun __merge(temp, l, m, r, comp)
        var k, i, j = l, l, m + 1

        while i <= m and j <= r do
            if comp(this[i], this[j]) <= 0 if comp else this[i] <= this[j] then
                temp[k] = this[i]
                i += 1
            else
                temp[k] = this[j]
                j += 1
            end
            k += 1
        end
        
        var size = #this
        while i < size and i <= m do
            temp[k] = this[i]
            i += 1; k += 1
        end

        for var i = l; i <= r; i += 1 do
            this[i] = temp[i]
        end
    end

    fun __string__()
        return "[" + ", ".join(this) + "]"
    end
end

class Tuple : Sequence
    native new(seq)
    native sub(from, to)
    native __len__()
    native __iter__(iter)
    native __next__(idx)

    fun __string__()
        return "(" + ", ".join(this) + ")"
    end
end

class Table 
    native __get__(key)
    native __set__(key, val)
    native __len__()
    native delete(key)
    native clear()
    native contains(key)
    native keys()
    native values()
    native __iter__(i)
    native __next__(i)
    native __string__()
end

//======================================
//========= Standard iterators =========
//======================================

class Iter
    fun new(indexable)
        this.indexable = indexable
    end

    fun __iter__(i)
        if i == null then
            return 0
        end
        if i < #this.indexable - 1 then
            return i + 1
        else 
            return false
        end
    end

    fun __next__(i)
        return this.indexable[i]
    end
end

class Reverse
    fun new(indexable)
        this.indexable = indexable
    end

    fun __iter__(i)
        if i == null then
            var idx = #this.indexable - 1
            return idx if idx >= 0 else false
        end
        if i > 0 then
            return i - 1
        else
            return false
        end
    end

    fun __next__(i)
        return this.indexable[i]
    end
end

class Enumerate
    fun new(iter, start=0)
        this.iter = iter
        this.idx = start
    end

    fun __iter__(i)
        return this.iter.__iter__(i)
    end

    fun __next__(i)
        var ret = this.idx, this.iter.__next__(i)
        this.idx += 1
        return ret
    end
end

class Zip
    fun new(...)
        if #args == 0 then 
            raise TypeException("Must provide at least one iterator.") 
        end
        this.iters = args
        this.result = []
    end

    fun __iter__(tup)
        if tup == null then
            for var iterator in this.iters do
                this.result.add(iterator.__iter__(null))
            end
        else
            var i = 0
            for var iterator in this.iters do
                var res = iterator.__iter__(tup[i])
                if !res then return false end
                this.result.add(res)
                i += 1
            end
        end

        var res = Tuple(this.result)
        this.result.clear()
        return res
    end

    fun __next__(tup)
        var i, res = 0, []
        for var iterator in this.iters do
            res.add(iterator.__next__(tup[i]))
        end
        return Tuple(res)
    end
end

//==================================
//========= Core functions =========
//==================================

fun assert(cond, msg="assertion failed")
    if !cond then raise AssertException(msg) end
end

fun sorted(iterable, comp=null)
    var lst = tolist(iterable)
    lst.sort(comp)
    return lst
end

fun toList(iterable)
    var lst = []
    for var e in iterable do
        lst.add(e)
    end
    return lst
end

native type(o)
native ascii(char)
native char(num)
native eval(source)
native int(n)
native isInt(n)
native num(n)

native print(s, ...)

//==============================
//========= Exceptions =========
//==============================

class Exception
    fun new(err=null)
        this.err = err
    end
end

class TypeException : Exception end
class NameException : Exception end
class FieldException : Exception end
class MethodException : Exception end
class ImportException : Exception end
class StackOverflowException : Exception end
class DivisionByZeroException : Exception end
class InvalidArgException : Exception end
class IndexOutOfBoundException : Exception end
class AssertException : Exception end
class NotImplementedException : Exception end